job "prometheus" {
  region      = "home"
  datacenters = ["dc1"]
  type        = "service"

  constraint {
    attribute = "${attr.unique.hostname}"
    operator  = "="
    value     = "nomad-2"
  }

  update {
    stagger      = "20s"
    max_parallel = 1
  }

  group "prometheus" {
    count = 1

    network {
      port "http" { static = 9090 }
    }

    task "prometheus" {
      driver = "docker"

      config {
        image        = "prom/prometheus:v2.27.1"
        network_mode = "host"
        ports        = ["http"]
        args = [
          "--config.file", "/local/prometheus.yml",
          "--storage.tsdb.path", "/data",
          "--web.listen-address", "0.0.0.0:9090"
        ]
        volumes = [
          "/mnt/disks/sdb/prometheus:/data",
        ]
      }

      user = "1000"

      service {
        name = "prometheus"
        port = "http"
        tags = [
          "traefik.enable=true",
          "traefik.http.middlewares.httpsRedirect.redirectscheme.scheme=https",
          "traefik.http.routers.prometheus_http.entrypoints=http",
          "traefik.http.routers.prometheus_http.middlewares=httpsRedirect",
          "traefik.http.routers.prometheus_http.rule=Host(`prometheus.home`)",
          "traefik.http.routers.prometheus_https.entrypoints=https",
          "traefik.http.routers.prometheus_https.rule=Host(`prometheus.home`)",
          "traefik.http.routers.prometheus_https.tls.domains[0].sans=prometheus.home",
        ]
        check {
          type     = "http"
          path     = "/-/healthy"
          name     = "http"
          interval = "5s"
          timeout  = "2s"
        }
      }

      resources {
        cpu    = 400
        memory = 4096
      }

      template {
        data = <<EOH
global:
  scrape_interval: 60s
  scrape_timeout: 10s
  evaluation_interval: 60s

scrape_configs:
  - job_name: prometheus
    static_configs:
      - targets: ["nomad-1.node.consul:9090"]

  - job_name: server-stats
    static_configs:
      - targets:
        - "nomad-1.node.consul:9100"
        - "nomad-2.node.consul:9100"
        - "nomad-3.node.consul:9100"

  - job_name: coredns
    static_configs:
      - targets: ["nomad-2.node.consul:9153", "nomad-3.node.consul:9153"]

  - job_name: pihole
    static_configs:
      - targets: ["pihole.service.consul"]

  - job_name: turbogeth
    static_configs:
      - targets: ["turbogeth.service.consul:6060"]

  - job_name: mikrotik
    static_configs:
      - targets: ["mikrotik-exporter.service.consul:9436"]
EOH

        destination   = "local/prometheus.yml"
        change_mode   = "signal"
        change_signal = "SIGHUP"
        env           = false
      }
    }
  }
}
